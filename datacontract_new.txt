-- =============================================
-- Author:		Kaushal Parik
-- Create date: 16-Dec-2013
-- Description:	SP to generate Data Contract
-- [usp_Dev_CreateDataContract] 'Task'
-- =============================================
ALTER PROCEDURE [dbo].[usp_Dev_CreateDataContract]
	@TableName VARCHAR(50)
AS
BEGIN
	DECLARE @SchemaDetail TABLE (
		Id			INT		IDENTITY(1,1),
		ColName		NVARCHAR(255),
		IsNullable	BIT,
		IsIdentity	BIT,
		ColType		NVARCHAR(255))
		
	DECLARE @RowCnt			INT
	DECLARE @Index			INT
	DECLARE @DataContract	VARCHAR(MAX)
	DECLARE @NewLineChar	AS CHAR(2) = CHAR(13) + CHAR(10)
	DECLARE @DefaultDescr VARCHAR(1000)
	
	-- header information of the DataContract Class
	SET @DataContract = ''
	SET @DataContract +='/// <summary>' + @NewLineChar
	SET @DataContract +='/// This Class holds the Property/DataMembers of ' + @TableName + ' Table.' + @NewLineChar
	SET @DataContract +='/// </summary>' + @NewLineChar
	SET @DataContract +='[DataContract (Name = "' + @TableName + 'DataContract' + '")]' + @NewLineChar
	SET @DataContract +='public class ' + @TableName + 'DataContract' + @NewLineChar
	SET @DataContract +='{' + @NewLineChar
	SET @RowCnt = 0
	SET @Index = 0
	
	DECLARE @ColName	NVARCHAR(255)
	DECLARE @IsNullable BIT
	DECLARE @IsIdentity BIT
	DECLARE @ColType	NVARCHAR(255)
	DECLARE @Property	NVARCHAR(255)
	
	INSERT INTO @SchemaDetail (	 ColName
								,IsNullable
								,IsIdentity
								,ColType)
	SELECT	 name
			,is_nullable
			,is_identity
			,TYPE_NAME(system_type_id) FROM sys.columns 
	WHERE OBJECT_NAME(object_id) = @TableName
	
	Set @RowCnt = @@ROWCOUNT  
	Set @Index	= 1
	WHILE @Index <= @RowCnt
	BEGIN
		SELECT 
				@ColName	= ColName,
				@IsNullable = IsNullable,
				@IsIdentity = IsIdentity,
				@ColType	= ColType 
		FROM @SchemaDetail 
		WHERE Id = @Index
		
		SET @Property = ''
		
		SET @DataContract +='/// <summary>' + @NewLineChar
		IF ISNULL(@IsIdentity,0) = 1
			SET @DataContract +='///The ' + @ColName + ' is an Identity Primary Key of the table.' + @NewLineChar
		ELSE
			BEGIN
				-- Get default description from schema property if exists
				SELECT @DefaultDescr = CONVERT(VARCHAR(1000),VALUE) FROM fn_listextendedproperty('MS_DESCRIPTION','schema','dbo','table',@TableName,'column',@ColName) INNER JOIN INFORMATION_SCHEMA.COLUMNS CC on objname collate SQL_Latin1_General_CP1_CI_AS = CC.COLUMN_NAME collate SQL_Latin1_General_CP1_CI_AS AND CC.COLUMN_NAME = @ColName AND CC.TABLE_NAME = @TableName
				SET @DataContract +='/// The field holds ' + @ColName + ' value. ' + ISNULL(@DefaultDescr,'') + @NewLineChar
			END
		SET @DataContract +='/// </summary>' + @NewLineChar
		
		IF ISNULL(@IsNullable,0) = 0
			SET @DataContract +='[DataMember(IsRequired = true)]' + @NewLineChar
		ELSE
			SET @DataContract +='[DataMember(IsRequired = false)]' + @NewLineChar
		
		SET @Property = 'public '
		IF ISNULL(@IsNullable,0) = 0
			SET @Property += CASE @ColType 
											WHEN 'bigint'		THEN 'Int64' 
											WHEN 'int'			THEN 'Int32' 
											WHEN 'tinyint'		THEN 'Int32' 
											WHEN 'smallint'		THEN 'Int16' 
											WHEN 'decimal'		THEN 'Decimal' 
											WHEN 'float'		THEN 'Double' 
											WHEN 'real'			THEN 'Single' 
											WHEN 'money'		THEN 'Decimal' 
											WHEN 'numeric'		THEN 'Decimal' 
											WHEN 'nvarchar'		THEN 'string' 
											WHEN 'nchar'		THEN 'string' 
											WHEN 'ntext'		THEN 'string' 
											WHEN 'text'			THEN 'string' 
											WHEN 'char'			THEN 'string' 
											WHEN 'varchar'		THEN 'string'  
											WHEN 'xml'			THEN 'string' 
											WHEN 'datetime'		THEN 'DateTime' 
											WHEN 'datetime2'	THEN 'DateTime' 
											WHEN 'date'			THEN 'DateTime' 
											WHEN 'time'			THEN 'TimeSpan' 
											WHEN 'bit'			THEN 'bool'
							 END 
		ELSE
			SET @Property += CASE @ColType	
											WHEN 'bigint'		THEN 'Int64?' 
											WHEN 'int'			THEN 'Int32?' 
											WHEN 'tinyint'		THEN 'Int32?' 
											WHEN 'smallint'		THEN 'Int16?' 
											WHEN 'decimal'		THEN 'Decimal?' 
											WHEN 'real'			THEN 'Single?' 
											WHEN 'float'		THEN 'Double?' 
											WHEN 'money'		THEN 'Decimal?'
											WHEN 'numeric'		THEN 'Decimal?'  
											WHEN 'nvarchar'		THEN 'string' 
											WHEN 'nchar'		THEN 'string' 
											WHEN 'ntext'		THEN 'string' 
											WHEN 'text'			THEN 'string' 
											WHEN 'char'			THEN 'string' 
											WHEN 'varchar'		THEN 'string' 
											WHEN 'xml'			THEN 'string' 
											WHEN 'datetime'		THEN 'DateTime?' 
											WHEN 'datetime2'	THEN 'DateTime?' 
											WHEN 'date'			THEN 'DateTime?' 
											WHEN 'time'			THEN 'TimeSpan?' 
											WHEN 'bit'			THEN 'bool?'
							 END 
							 
		SET @Property += ' ' + @ColName
		SET @Property += '{ get; set; }'  + @NewLineChar + @NewLineChar
		SET @DataContract += @Property
		SET @Index = @Index + 1
		print @DataContract
		SET @DataContract = ''
	END
	SET @DataContract +='}' + @NewLineChar
	PRINT @DataContract
END
